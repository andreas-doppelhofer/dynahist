//environment variables: 'BINTRAY_KEY', 'BINTRAY_USER', 'BINTRAY_VERSION' and 'GITHUB_TOKEN'
//release task: 'gradle --build-file release.gradle bintrayUpload'

buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1+'
    }
}

plugins{
    id 'com.jfrog.bintray' version '1.7.3'
    id 'java'
    id 'maven-publish'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

repositories {
    jcenter()
}

jar{
    manifest {
        attributes 'Specification-Title': 'DynaHist',
                'Specification-Vendor': 'Dynatrace LLC',
                'url': 'https://github.com/dynatrace-oss/dynahist'
    }
}

javadoc{
    title 'DynaHist ' + System.getenv('BINTRAY_VERSION') + ' API'
}

assemble{
    version System.getenv('BINTRAY_VERSION')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            artifactId 'dynahist'
            groupId = 'com.dynatrace.dynahist'
            version = System.getenv('BINTRAY_VERSION')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    publish = true
    // override = true // uncomment to overwrite the release
    pkg {
        userOrg = 'dynatrace'
        repo = 'dynahist'
        name = 'com.dynatrace.dynahist'
        desc = 'DynaHist: A Dynamic Histogram Library for Java'
        websiteUrl = 'https://github.com/dynatrace-oss/dynahist'
        issueTrackerUrl = 'https://github.com/dynatrace-oss/dynahist/issues'
        vcsUrl = 'https://github.com/dynatrace-oss/dynahist.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = System.getenv('BINTRAY_VERSION')
            vcsTag = System.getenv('BINTRAY_VERSION')
            released  = new Date()
        }
    }
}

githubRelease {
    token System.getenv('GITHUB_TOKEN')
    owner 'dynatrace-oss'
    repo 'dynahist'
    tagName 'release-' + System.getenv('BINTRAY_VERSION')
    releaseName 'release-' +  System.getenv('BINTRAY_VERSION')
    body 'https://github.com/dynatrace-oss/dynahist/blob/master/CHANGELOG.md'
    FilenameFilter filter = { dir, filename -> filename.contains(System.getenv('BINTRAY_VERSION')) }
    releaseAssets = jar.destinationDir.listFiles filter
    // overwrite true // uncomment to overwrite the release
}

tasks.bintrayUpload.dependsOn(tasks.assemble)
tasks.bintrayUpload.finalizedBy tasks.githubRelease