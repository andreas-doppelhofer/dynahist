plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'me.champeau.gradle.jmh' version '0.5.3'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.0'
    jmh group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.12'
    jmh group: 'com.datadoghq', name: 'sketches-java', version: '0.6.0'
    jmh group: 'org.openjdk.jol', name: 'jol-core', version: '0.14'
}

license {
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    header = rootProject.file('docs/licenses/HEADER.txt')
    strictCheck = true
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Dynatrace LLC'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jmh {
    timeUnit = 'ms'
}

task copyBenchmarkReport(type: Copy) {
    rename ('results.txt', 'results ' + new Date().dateString.replaceAll('/','-') + ' ' + new Date().timeString.replaceAll(':', '-') +'.txt')
    from file('build/reports/jmh/results.txt')
    into file('build/reports/jmh/results/')
}

sourceSets {
    jmh
    main
    test
}

task runSpaceConsumptionTest(type: JavaExec) {
    group = "Benchmark"
    description = "Runs the space consumption test"
    classpath = sourceSets.jmh.runtimeClasspath
    main = "com.dynatrace.dynahist.SpaceConsumptionTest"
}

tasks.test.finalizedBy jacocoTestReport
tasks.jmh.finalizedBy copyBenchmarkReport